 
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Свойство_МеткаЗадачи = Справочники.узКонстанты.ПолучитьЗначениеКонстанты(
		"Свойство_МеткаЗадачи", 
		Тип("ПланВидовХарактеристикСсылка.узДопРеквизитыЗадачМножественные"), , Истина, Истина);	
		
	ДобавитьМеткиНаФорму();		
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомандаМетки(Команда) Экспорт
	
	ЭлементФормы = Элементы[Команда.Имя];
	
	ЭлементФормы.Пометка = НЕ ЭлементФормы.Пометка;
	
	МеткаЗаголовок = ЭлементФормы.Заголовок;
	 	
	ПереместитьКнопкуМетки(Команда.Имя, МеткаЗаголовок);
		
КонецПроцедуры                                                         

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбъектФормыВладельца = ЭтотОбъект.ВладелецФормы.Объект;
	
	СтрокиКУдалению = Новый Массив();
	
	Для Каждого ЭлСпискаМетки Из МеткиСписок Цикл
		
		МеткаСсылка = ЭлСпискаМетки.Значение;
		
		ЕстьМеткаВОбъекте = Ложь;
		
		Для Каждого СтрокаДопРеквизитыМножественные Из ОбъектФормыВладельца.ДопРеквизитыМножественные Цикл
			
			МеткаВОбъекте = СтрокаДопРеквизитыМножественные.Значение;
			
			Если МеткаВОбъекте <> МеткаСсылка Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьМеткаВОбъекте = Истина;
			
			Если НЕ ЭлСпискаМетки.Пометка Тогда
				СтрокиКУдалению.Добавить(СтрокаДопРеквизитыМножественные);	
			КонецЕсли;
				
		КонецЦикла;
		
		Если ЭлСпискаМетки.Пометка 
			И НЕ ЕстьМеткаВОбъекте Тогда
			
			СтрокаДопРеквизитыМножественные = ОбъектФормыВладельца.ДопРеквизитыМножественные.Добавить();	
			СтрокаДопРеквизитыМножественные.Свойство = Свойство_МеткаЗадачи;
			СтрокаДопРеквизитыМножественные.КлючСтроки = "" + Новый УникальныйИдентификатор;
			СтрокаДопРеквизитыМножественные.Значение = МеткаСсылка;
			
		КонецЕсли;  
		
	КонецЦикла;

	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ОбъектФормыВладельца.ДопРеквизитыМножественные.Удалить(СтрокаКУдалению);		
	КонецЦикла;	
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьМеткиНаФорму()
	
	МеткиЗадачи = Параметры.МеткиЗадачи;
	
	ВсеМеткиЗадач = Справочники.узМеткиЗадач.ПолучитьВсеМеткиЗадач();
	
	НомерМетки = 1;
	
	ДействиеКоманды = "КомандаМетки";
	ЭлементГруппаМеткиВыбранные = Элементы.ГруппаМеткиВыбранные;
	ЭлементГруппаМеткиДоступные = Элементы.ГруппаМеткиДоступные;
	
	Для Каждого МеткаСсылка Из ВсеМеткиЗадач Цикл
			
		МеткаВыбрана = Ложь;
		ЭлементРодитель = ЭлементГруппаМеткиДоступные; 
		Если МеткиЗадачи.Найти(МеткаСсылка) <> Неопределено Тогда
			МеткаВыбрана = Истина;                     
			ЭлементРодитель = ЭлементГруппаМеткиВыбранные; 
		КонецЕсли;                      
		
		МеткиСписок.Добавить(МеткаСсылка, , МеткаВыбрана);
		
		ИмяКоманды = "КомандаМетка" + НомерМетки;		
		
		ЗаголовокМетки = "" + МеткаСсылка;
		
		Команда = Команды.Добавить(ИмяКоманды);
		Команда.Действие = ДействиеКоманды;
		Команда.Заголовок = ЗаголовокМетки;
			
		КнопкаФормы = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементРодитель);
		КнопкаФормы.Заголовок = ЗаголовокМетки; 
		КнопкаФормы.ИмяКоманды = ИмяКоманды;
		КнопкаФормы.Фигура = ФигураКнопки.Овал;
		КнопкаФормы.АвтоМаксимальнаяШирина = Истина;    
		КнопкаФормы.ЦветФона = МеткаСсылка.ЦветФонаМетки.Получить();
		КнопкаФормы.ЦветТекста = МеткаСсылка.ЦветТекстаМетки.Получить();
		КнопкаФормы.Шрифт = Новый Шрифт("@Arial Unicode MS", 8, Ложь);
		
		КнопкаФормы.Пометка = Ложь;			
		Если МеткаВыбрана Тогда
			КнопкаФормы.Пометка = Истина; 
		КонецЕсли;		
		
		НомерМетки = НомерМетки + 1;
		
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьВидимостьДоступность()            
	
	Элементы.ДекорацияДоступные.Видимость = Ложь;

	Если Элементы.ГруппаМеткиДоступные.ПодчиненныеЭлементы.Количество() > 0 Тогда
		Элементы.ДекорацияДоступные.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура ПереместитьКнопкуМетки(ИмяЭлемента, МеткаЗаголовок) 
	
	ЭлементФормы = Элементы[ИмяЭлемента];
	
	Если ЭлементФормы.Пометка Тогда
		Элементы.Переместить(ЭлементФормы, Элементы.ГруппаМеткиВыбранные); 
	Иначе
		Элементы.Переместить(ЭлементФормы, Элементы.ГруппаМеткиДоступные);
	КонецЕсли;     
	
	МеткаСсылка = Справочники.узМеткиЗадач.НайтиПоНаименованию(МеткаЗаголовок, Истина);	 
	
	ЭлСписка = МеткиСписок.НайтиПоЗначению(МеткаСсылка);
	ЭлСписка.Пометка = ЭлементФормы.Пометка;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры    

#КонецОбласти
