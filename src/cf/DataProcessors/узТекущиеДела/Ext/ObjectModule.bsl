Процедура ЗаполнитьТекущиеДела() Экспорт
	ТЧТекущиеДела.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	узТекущиеДела.Ссылка КАК ТекущееДело,
	|	узТекущиеДела.Выполнено КАК Выполнено,
	|	узТекущиеДела.ТекстСодержания,
	|	узТекущиеДела.Автор,
	|	узТекущиеДела.ДатаСоздания,
	|	узТекущиеДела.ДатаВыполнения,
	|	узТекущиеДела.Задача,
	|	узТекущиеДела.Вопрос,
	|	узТекущиеДела.Порядок КАК Порядок
	|ИЗ
	|	Справочник.узТекущиеДела КАК узТекущиеДела
	|ГДЕ
	|	узТекущиеДела.Автор = &Автор
	|	И ВЫБОР
	|			КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаСоздания, ДЕНЬ) > &НаДату ТОГДА
	|				ЛОЖЬ	
	|			КОГДА узТекущиеДела.ДатаВыполнения = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаВыполнения, ДЕНЬ) = &НаДату
	|				ТОГДА ИСТИНА
	|			КОГДА НАЧАЛОПЕРИОДА(узТекущиеДела.ДатаСоздания, ДЕНЬ) = &НаДату
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Выполнено,
	|	Порядок";
	
	Запрос.УстановитьПараметр("Автор", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧТекущиеДела = ТЧТекущиеДела.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧТекущиеДела,Выборка);
	КонецЦикла;
КонецПроцедуры 

Процедура СохранитьТекущиеДела() Экспорт
	Для каждого СтрокаТЧТекущиеДела из ТЧТекущиеДела цикл
		СтрокаТЧТекущиеДела.ТекстСодержания = СокрЛП(СтрокаТЧТекущиеДела.ТекстСодержания);
		Если НЕ ЗначениеЗаполнено(СтрокаТЧТекущиеДела.ТекстСодержания) Тогда
			Продолжить;
		Конецесли;
		
		ТекущееДелоОбъект = ПолучитьТекущееДелоОбъект(СтрокаТЧТекущиеДела); 
		ЗаполнитьЗначенияСвойств(ТекущееДелоОбъект,СтрокаТЧТекущиеДела,,"Автор,ДатаСоздания");
		ТекущееДелоОбъект.Наименование = ТекущееДелоОбъект.ТекстСодержания;		
		ТекущееДелоОбъект.Порядок = СтрокаТЧТекущиеДела.НомерСтроки;
		Если СтрокаТЧТекущиеДела.Выполнено = Ложь Тогда
			ТекущееДелоОбъект.ДатаВыполнения = Дата(1,1,1);
		Конецесли;
		
		ТекущееДелоОбъект.Записать();
	Конеццикла;
КонецПроцедуры 

Функция ПолучитьТекущееДелоОбъект(СтрокаТЧТекущиеДела) 
	Перем ТекущееДелоОбъект;
	
	пТекущееДело = СтрокаТЧТекущиеДела.ТекущееДело;
	
	Если ЗначениеЗаполнено(пТекущееДело) Тогда
		ТекущееДелоОбъект = пТекущееДело.ПолучитьОбъект();
		Возврат ТекущееДелоОбъект;		
	Конецесли;
	
	ТекущееДелоОбъект = СоздатьТекущееДело(СтрокаТЧТекущиеДела);
	
	Возврат ТекущееДелоОбъект;
КонецФункции 

Функция СоздатьТекущееДело(СтрокаТЧТекущиеДела) 
	ТекущееДелоОбъект = Справочники.узТекущиеДела.СоздатьЭлемент();
	ТекущееДелоОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
	ТекущееДелоОбъект.ДатаСоздания = ТекущаяДата();
	
	Возврат ТекущееДелоОбъект;
КонецФункции 

Процедура УбратьТекущееДело(МассивТекущихДел) Экспорт
	Для каждого пТекущееДело из МассивТекущихДел цикл
		ТекущееДелоОбъект = пТекущееДело.ПолучитьОбъект();
		ТекущееДелоОбъект.Удалить();			
	Конеццикла;	
КонецПроцедуры 

Процедура ОбновитьНаСервере() Экспорт
	СохранитьТекущиеДела();
	ЗаполнитьТекущиеДела();
КонецПроцедуры 
