//начало текста модуля

///////////////////////////////////////////////////
//Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Behavior
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Behavior.
Перем КонтекстСохраняемый Экспорт;

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПолеФормыСИменемСталоРавным(Парам01,Парам02)","ПолеФормыСИменемСталоРавным","Тогда поле формы с именем ""ЧасыФакт"" стало равным '15'");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯПодготавливаюКонстантуСНаименованием(Парам01)","ЯПодготавливаюКонстантуСНаименованием","Допустим я подготавливаю константу с наименованием ""узИспользоватьУчетВремени""");
	
	Возврат ВсеТесты;
КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Процедура выполняется перед началом каждого сценария
Процедура ПередНачаломСценария() Экспорт
	СостояниеVanessaBehavior = Ванесса.ПолучитьСостояниеVanessaBehavior();
	ИмяСценария = СостояниеVanessaBehavior.ТекущийСценарий.Имя;
	Если ИмяСценария = "Проверка вывода ""ЧасыПлан"", ""ЧасыФакт"", ""ЧасыКОплате"" в отчете ""узОтчетПоЗадачам""" Тогда
		
		ПредставлениеСправочника = "узЗадачи";
		СоздатьЭлементСправочникаСНаименованиеНаСервере(ПредставлениеСправочника, "ЭтоТестоваяЗадача");
		ЭлементСправочника = утвПолучитьЭлементГруппуСправочника(ПредставлениеСправочника, , "ЭтоТестоваяЗадача",,ИСТИНА);
		
		УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, "ЧасыПлан"	 , 10);
		УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, "ЧасыФакт"	 , 15);
		УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, "ЧасыКОплате" , 10);
		УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, "ПоказыватьВОтчетахИКанбанДоске", ИСТИНА);
		УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, "ДатаСоздания", Дата(2016,12,19,23,17,58));
		
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
// Процедура выполняется перед окончанием каждого сценария
Процедура ПередОкончаниемСценария() Экспорт
	СостояниеVanessaBehavior = Ванесса.ПолучитьСостояниеVanessaBehavior();
	ИмяСценария = СостояниеVanessaBehavior.ТекущийСценарий.Имя;
	Если ИмяСценария = "Проверка вывода ""ЧасыПлан"", ""ЧасыФакт"", ""ЧасыКОплате"" в отчете ""узОтчетПоЗадачам""" Тогда
		ПредставлениеСправочника = "узЗадачи";
		ЭлементСправочника = утвПолучитьЭлементГруппуСправочника(ПредставлениеСправочника, , "ЭтоТестоваяЗадача",,ИСТИНА);
		утвУдалитьЭлементСправочника(ПредставлениеСправочника, ЭлементСправочника);
	КонецЕсли; 
КонецПроцедуры

///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаСервере
Процедура СоздатьЭлементСправочникаСНаименованиеНаСервере(ИмяСправочника, Наименование)
	ЭлементСправочника = Справочники[ИмяСправочника].СоздатьЭлемент();
	ЭлементСправочника.Наименование = Наименование;
	ЭлементСправочника.Записать();
КонецПроцедуры

&НаСервере
Функция утвПолучитьЭлементГруппуСправочника(ИмяСправочника, Код = "", Наименование = "", ИскатьГруппу = Ложь, ДолженБыть = ЛОЖЬ)
	ПредставлениеПоиска = "";
	
	Если Истина
		И ПустаяСтрока(Код)                      
		И ПустаяСтрока(Наименование)
		Тогда
		
		ВызватьИсключение "Не заполнено ни одно свойство поиска";
		
	ИначеЕсли Истина
		И ЗначениеЗаполнено(Код)
		И ЗначениеЗаполнено(Наименование)
		Тогда
		
		ПредставлениеПоиска = "коду """ + Код + """ и наименованию """ + Наименование + """";
		
	ИначеЕсли ЗначениеЗаполнено(Код) Тогда
		ПредставлениеПоиска = "коду """ + Код + """";
		
	ИначеЕсли ЗначениеЗаполнено(Наименование) Тогда
		ПредставлениеПоиска = "наименованию """ + Наименование + """";
		
	КонецЕсли;
	
	ТекстИсключения = "Не нашли #ГруппуИлиЭлемент справочника #ИмяСправочника по #ПредставлениеПоиска";
	
	ТекстИсключения = СтрЗаменить(ТекстИсключения, "#ГруппуИлиЭлемент"	 , ?(ИскатьГруппу, "группу", "элемент"));
	ТекстИсключения = СтрЗаменить(ТекстИсключения, "#ИмяСправочника"	 , ИмяСправочника);
	ТекстИсключения = СтрЗаменить(ТекстИсключения, "#ПредставлениеПоиска", ПредставлениеПоиска);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Справочник.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Справочник
	|ГДЕ
	|	&МоиУсловия";
	
	ТекстМоиУсловия = "";
	
	Если ИскатьГруппу Тогда
		СформироватьТекстУсловияЗапроса(ТекстМоиУсловия, "Справочник.ЭтоГруппа = &ИскатьГруппу");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Код) Тогда
		СформироватьТекстУсловияЗапроса(ТекстМоиУсловия, "Справочник.Код = &Код");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Наименование) Тогда
		СформироватьТекстУсловияЗапроса(ТекстМоиУсловия, "Справочник.Наименование = &Наименование");
	КонецЕсли; 
	
	Если ПустаяСтрока(ТекстМоиУсловия) Тогда
		ТекстМоиУсловия = "ИСТИНА";
	КонецЕсли; 
	
	СтрокаЗаменыСправочника = "Справочник." + ИмяСправочника + " КАК Справочник";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&МоиУсловия", ТекстМоиУсловия);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.Пользователи КАК Справочник", СтрокаЗаменыСправочника);
	
	Запрос.УстановитьПараметр("ИскатьГруппу", ИскатьГруппу);
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если ДолженБыть И РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();

	Возврат ВыборкаДетальныеЗаписи.Ссылка;
КонецФункции

&НаСервере
Процедура СформироватьТекстУсловияЗапроса(ТекстРезультат, ТекстУсловия)
	Если НЕ ПустаяСтрока(ТекстРезультат) Тогда
		ТекстРезультат = ТекстРезультат + " И "
	КонецЕсли; 
	ТекстРезультат = ТекстРезультат + ТекстУсловия;
КонецПроцедуры

&НаСервере
Процедура утвУдалитьЭлементСправочника(ИмяСправочника, ЭлементСправочника)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Справочник.Ссылка
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК Справочник
	|ГДЕ
	|	Справочник.Ссылка = &ЭлементСправочника";
	
	Запрос.УстановитьПараметр("ЭлементСправочника", ЭлементСправочника);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЭлементОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		
		ЭлементОбъект.Удалить();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеРеквизитаСправочника(ЭлементСправочника, ИмяРеквизита, Значение, ЗначениеВХранилище = ЛОЖЬ)
	СпрОбъект = ЭлементСправочника.ПолучитьОбъект();
	Если ЗначениеВХранилище Тогда
		СпрОбъект[ИмяРеквизита] = Новый ХранилищеЗначения(Значение);
	Иначе
		СпрОбъект[ИмяРеквизита] = Значение;
	КонецЕсли;
	СпрОбъект.Записать();
КонецПроцедуры

&НаКлиенте
Функция ПолучитьАктивноеОкноИзТестовоеПриложение()
	Возврат КонтекстСохраняемый.ТестовоеПриложение.ПолучитьАктивноеОкно();
КонецФункции	

&НаКлиенте
Функция ПолучитьАктивноеОкноИзКонтекста()
	Возврат Контекст.АктивноеОкно;
КонецФункции	

&НаКлиенте
Функция НайтиПолеПоИмени(ИмяПоля,НужнаяФорма = Неопределено)
	Получилось = Ложь;
	Если Не Получилось Тогда
		Если НужнаяФорма = Неопределено Тогда
			ОкноПриложения         = ПолучитьАктивноеОкноИзТестовоеПриложение();
			НужнаяФорма           = ОкноПриложения.НайтиОбъект(Тип("ТестируемаяФорма"));
		КонецЕсли;	 
		
		Если НужнаяФорма = Неопределено Тогда
			//иногда 1С некорректно отдаёт текущее окно, тогда будем искать во всех окнах
			//сначала поищем в недавно открытом окне
			
			Попытка
				ОкноПриложения = ПолучитьАктивноеОкноИзКонтекста();
				НужнаяФорма    = ОкноПриложения.НайтиОбъект(Тип("ТестируемаяФорма"));
			Исключение
				
			КонецПопытки;
			
			//Если НужнаяФорма = Неопределено Тогда
			//	//затем будем искать во всех окнах
			//	МассивОкон = КонтекстСохраняемый.ТестовоеПриложение.НайтиОбъекты(Тип("ТестируемоеОкноКлиентскогоПриложения"));
			//	Для каждого ТекОкно Из МассивОкон Цикл
			//		
			//		НужнаяФорма    = ТекОкно.НайтиОбъект(Тип("ТестируемаяФорма"));
			//		Если НужнаяФорма <> Неопределено Тогда
			//			Контекст.Вставить("АктивноеОкно",ОкноПриложения); //произошла неявная смена активного окна
			//		КонецЕсли;	 
			//		
			//	КонецЦикла;
			//	
			//КонецЕсли;	 
		КонецЕсли;	 
		
		
		Если ИмяПоля = "" Тогда
			Поле = НужнаяФорма.НайтиОбъект(Тип("ТестируемоеПолеФормы"));
		Иначе	
			Поле = НужнаяФорма.НайтиОбъект(Тип("ТестируемоеПолеФормы"),,ИмяПоля);
		КонецЕсли;	 
		
	КонецЕсли;	 
	
	Возврат Поле;
КонецФункции	

&НаСервере
Процедура УстановитьЗначениеКонстаны(ИмяКонстанты, Результат)
	Константы[ИмяКонстанты].Установить(Результат);
КонецПроцедуры

&НаКлиенте
//Допустим я подготавливаю константу с наименованием "узИспользоватьУчетВремени"
//@ЯПодготавливаюКонстантуСНаименованием(Парам01)
Процедура ЯПодготавливаюКонстантуСНаименованием(ИмяКонстанты) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	СостояниеVanessaBehavior = Ванесса.ПолучитьСостояниеVanessaBehavior();
	ИмяСценария = СостояниеVanessaBehavior.ТекущийСценарий.Имя;
	Если ИмяСценария = "Включение механизма ""Учет времени по задачам""" Тогда
		УстановитьЗначениеКонстаны(ИмяКонстанты, ЛОЖЬ);
	Иначе
		УстановитьЗначениеКонстаны(ИмяКонстанты, ИСТИНА);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
//Тогда поле формы с именем "ЧасыФакт" стало равным '15'
//@ПолеФормыСИменемСталаРавным(Парам01,Парам02)
Процедура ПолеФормыСИменемСталоРавным(ИмяПоля,Результат) Экспорт
	//Ванесса.ПосмотретьЗначение(Парам01,Истина);
	Поле = НайтиПолеПоИмени(ИмяПоля);
	ДанныеПоля = Поле.ПолучитьПредставлениеДанных();
	
	Ванесса.ПроверитьРавенство(ДанныеПоля,Результат,"Ожидаемое значение поля.");
КонецПроцедуры

//окончание текста модуля